using gold.structures.automaton.*
using gold.visualization.automaton.*

var counter: Integer

procedure main(args:String[]) begin
	Q := {"q_", "q0", "q1", "q2", "q3", "q4", "q5",
		  "q$", "B"}
	Σ := ('a'‥'z')∪{' ', '$'}
	Γ := ('a'‥'z')∪{'*', '$', '@', '#'}∪('0'‥'9')
	q := "q1"
	F := {"q$"}
	
	A := GDeterministicTransducer(Q, Σ, Γ, q, F, δ, g, h)
	GAutomataFrame.show(A)
end

function δ(q, σ) begin
	
	// Acepta que el primer caracter sea una letra o un espacio
	if q = "q0" and σ in ('a'‥'z') then
        return "q2"
    elseif q = "q0" and σ = ' ' then
    	counter := 1
        return "q1"
	
	elseif q = "q1" and σ in ('a'‥'z') then
        return "q2"
    elseif q = "q2" and σ in ('a'‥'z') then
        return "q3"
    elseif q = "q3" and σ in ('a'‥'z') then
        return "q4"
    elseif q = "q4" and σ in ('a'‥'z') then
        return "q5"
    elseif q = "q5" and σ in ('a'‥'z') then
    	counter := counter + 1
        return "q2"

    elseif q in {"q2", "q3", "q4", "q5"} and σ = ' ' then
    	counter := 1
        return "q1"
    elseif q in {"q2", "q3", "q4", "q5"} and σ = '$' then
        return "q$"
 
    else
        return "B"
    end

end

function g(q) begin
	return λ
end

function h(q, σ) begin
	if q in {"q0", "q1", "q2", "q3", "q4"} and σ in ('a'‥'z') then
        return σ
    
    elseif q = "q2" and σ = ' ' then
        return "###"+counter+"*"
    elseif q = "q3" and σ = ' ' then
        return "##"+counter+"*"
    elseif q = "q4" and σ = ' ' then
        return "#"+counter+"*"
    elseif q = "q5" and σ = ' ' then
        return counter+"*"
        
    elseif q = "q2" and σ = '$' then
        return "###"+counter+"$" // concat 1
    elseif q = "q3" and σ = '$' then
        return "##"+counter+"$"
    elseif q = "q4" and σ = '$' then
        return "#"+counter+"$"
    elseif q = "q5" and σ = '$' then
        return counter+"$"

    elseif q = "q5" and σ in ('a'‥'z') then
    	return "@"+σ
    	
    else
    	return λ
    end

end